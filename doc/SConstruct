Import('env')

doc_env = env.Clone()

# doxygen

doxy_cmd = doc_env.Command(
    "doxygen/html/index.html",
    "doxygen.conf",
    action="doxygen -q $SOURCE")
env.AlwaysBuild(doxy_cmd)
env.Alias("doxygen", doxy_cmd)

# man page

# this is generated in the source tree!
man_cmd = doc_env.Command(
    "#doc/nst.1",
    "nst.1.adoc",
    action=f"a2x -f manpage -D ./doc $SOURCE"
)

env.Alias("man", man_cmd)

# terminfo install tree

terminfo_dir = env['buildroot'] + '/doc/terminfo'

tic_cmd = doc_env.Command(
    "terminfo/s/st",
    "nst.tinfo",
    action=[
        Mkdir(terminfo_dir),
        f"tic -o{terminfo_dir} -sx doc/nst.tinfo"
    ]
)

env.Alias("install", tic_cmd)

# terminfo user based install

Default(tic_cmd)

tic_user_cmd = doc_env.Command(
    None,
    "nst.tinfo",
    action="tic -sx doc/nst.tinfo"
)

env.Alias("terminfo_user", tic_user_cmd)
env.AlwaysBuild(tic_user_cmd)
