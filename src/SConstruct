Import('env')

# always use the in-tree TCLAP for now, we rely on version 1.4 which isn't
# packaged yet in most distros (it's still a development branch).

# if env['use_system_pkgs']:
#     base_pkgs = ['tclap']
# else:
#     env.Append(CPPPATH=['#/tclap/include'])
#     base_pkgs = []

base_pkgs = []
current_version = env.GetCurrentGitTag("nst")
env.AddVersionFileTarget("nst", current_version)

env.Append(CPPPATH=['#/tclap/include'])
env.Append(CCFLAGS=[f'-DNST_VERSION=\\"{current_version.strip("v")}\\"'])


nst_env = env.Clone()
sources = nst_env.GatherSources(suffixes=['.cxx'], recursive=False)

# NOTE: libxpp must comes first here for static linking order
nst_env.ConfigureForLibOrPackage('libxpp', sources)
nst_env.ConfigureForLibOrPackage('libcosmos', sources)
nst_env.ConfigureForPackage(['xft', 'freetype2', 'fontconfig', 'x11'] + base_pkgs)

nst = nst_env.Program('nst', sources)

env['bins']['nst'] = nst
env.Alias('nst', nst)

msg_env = env.Clone()
sources = ['msg/main.cxx']
msg_env.ConfigureForLibOrPackage('libcosmos', sources)
msg_env.ConfigureForPackage(base_pkgs)
msg_env.Append(CPPPATH=['#/src'])
nst_msg = msg_env.Program('nst-msg', sources)

env['bins']['nst-msg'] = nst_msg
env.Alias('nst-msg', nst_msg)
